apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

dependencies {
    testCompile 'info.cukes:cucumber-java:1.1.8'
    testCompile 'info.cukes:cucumber-junit:1.1.8'
    testCompile 'junit:junit:4.11'
    testCompile 'org.seleniumhq.selenium:selenium-java:2.42.2'
    testCompile 'io.appium:java-client:1.3.0'
}

task assembleAndroidApp(dependsOn: ':android_app:assembleRelease') << {
    description "Assemble the Android APK binraries to be tested"
    copy {
        from project(':android_app').files('build/outputs/apk/')
        into file('bin')
        include '*.apk'
    }
}

task cucumber() {
    description "Runs BBD cucumber acceptance tests"
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['-f', 'pretty', '--glue', 'com.example', 'src/test/resources']
        }
    }
}

/* Add build dependencies and hooks here */
// Make sure we build the application before the start running the tests
testClasses.dependsOn assembleAndroidApp
